plugins {
	id "fabric-loom"
	id "info.u_team.gradle_files"
	id "me.modmuss50.mod-publish-plugin"
	id "maven-publish"
}

gradlefiles.loaderSuffix = "fabric"

group = rootProject.group
base.archivesName = gradlefiles.archivesName()
version = gradlefiles.version()

sourceSets {
	main {
		java {
			srcDirs += gradlefiles.project("common").sourceSets.common.java.srcDirs
		}
		resources {
			srcDirs += gradlefiles.project("common").sourceSets.common.resources.srcDirs
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
	withJavadocJar()
}

loom {
	accessWidenerPath = file("src/main/resources/gjeb.accesswidener")
	
	runs {
		client {
			runDir "run/client"
			vmArgs "-Dmixin.debug.verbose=true"
			vmArgs "-Dmixin.debug.export=true"
			ideConfigGenerated true
		}
		
		server {
			runDir "run/server"
			vmArgs "-Dmixin.debug.verbose=true"
			vmArgs "-Dmixin.debug.export=true"
			ideConfigGenerated true
		}
	}
	
	mixin {
		defaultRefmapName = "gjeb.mixins.refmap.json"
	}
}

tasks.withType(Jar).configureEach { task ->
	gradlefiles.defaultJar(task)
}

tasks.withType(Javadoc).configureEach { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

signjar.sign(tasks.named("remapJar"))

processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
			file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
		}
	}
}

repositories {
	maven {
		url = "https://maven.parchmentmc.org"
	}
	maven {
		url = "https://maven.terraformersmc.com"
	}
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	minecraft gradlefiles.fabricMinecraftDependency()
	mappings loom.layered() {
		officialMojangMappings()
		parchment(config.fabric.mapping_layer)
	}
	
	modImplementation gradlefiles.fabricLoaderDependency()
	modRuntimeOnly gradlefiles.fabricApiDependency()
	
	modRuntimeOnly "com.terraformersmc:modmenu:${config.modmenu.fabric.version}"
	
	modRuntimeOnly "maven.modrinth:sodium:mc1.21-0.5.11"
}

publishMods {
	file = tasks.named("remapJar").flatMap { jar -> jar.archiveFile }
	displayName = "${config.publish.display}-Fabric-${config.minecraft.version}-${project.version}"
	changelog = "[Changelog for version ${project.version}](${gradlefiles.changelogUrl()})"
	type = me.modmuss50.mpp.ReleaseType.of(config.publish.type)
	modLoaders.add("fabric")
	
	curseforge {
		accessToken = defaultPropertyValue("curseforge.token")
		
		projectId = config.curse.id
		projectSlug = config.curse.slug
		minecraftVersions.add(config.minecraft.version)
		javaVersions.add(JavaVersion.VERSION_21)
		clientRequired = true
		
		optional("u-team-core")
		optional("sodium")
	}
	
	modrinth {
		accessToken = defaultPropertyValue("modrinth.token")
		
		projectId = config.modrinth.id
		minecraftVersions.add(config.minecraft.version)
		
		optional("u-team-core")
		optional("sodium")
	}
	
	discord {
		webhookUrl = defaultPropertyValue("discord.update_webhook")
		
		username = "Mod Update"
		avatarUrl = "https://raw.githubusercontent.com/MC-U-Team/${config.github.name}/${config.github.branch}/logo.png"
		
		content = changelog.map { log -> "# ${config.minecraft.version}: ${config.publish.display} (Fabric)\n" + log}
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId project.base.archivesName.get()
			
			from gradlefiles.filteredJavaComponent {
				filterDependency { holder ->
					"sodium" == holder.dependency.name
				}
				filterDependency { holder ->
					"modmenu" == holder.dependency.name
				}
			}
			
			pom {
				pom.name = project.base.archivesName.get()
				rootProject.commonPom(it)
			}
		}
	}
}